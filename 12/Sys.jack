// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {
    static int FREQUENCY;

    /** Performs all the initializations required by the OS. */
    function void init() {
        let FREQUENCY = 9999;

        // Array and String are not collections of static methods,
        // hence we don't need to call their init()
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        do Main.main();
        // Prevent the CPU from executing whatever instructions come next in memory:
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while (true) { }
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i;

        let i = 0;
        while (i < FREQUENCY * duration) {
            let i = i + 1;
        }

        return;

    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {

        do Output.printString("ERR<");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}