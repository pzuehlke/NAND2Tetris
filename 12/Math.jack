// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array twoToThe; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int i, p;
        let n = 16;
        let twoToThe = Array.new(16);
        
        let twoToThe[0] = 1;
        let i = 0;
        let p = 1;
        while (i < 16) {
            let twoToThe[i] = p;
            let p = p + p;
            let i = i + 1;
        }
        return;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, shiftedx, i;

        let sum = 0;
        let shiftedx = x;
        let i = 0;

        // No need to worry about overflow in the computation of sum and of shiftedx
        // as per the video lectures (operations are correct modulo 2^16).
        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedx;
            }
            let shiftedx = shiftedx + shiftedx;
            let i = i + 1;
        }

        return sum;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int q, result;
        var boolean sameSign;

        let sameSign = ((x > 0) = (y > 0));  // true iff x, y have same sign
        let x = Math.abs(x);
        let y = Math.abs(y);

        if (y = 0) {  // division by zero!
            do Output.printString("Error: Division by zero");
            do Sys.error(0);  // Division by zero
            return 0;
        }

        if ((x < y) | (y < 0)) {
            return 0;
        }

        let q = Math.divide(x, y + y);

        if ((x - (2 * q * y)) < y) {
            let result = q + q;
        } else {
            let result = q + q + 1;
        }
        
        if (sameSign) {
            return result;
        } else {
            return -result;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, approx, approxSquared;

        let y = 0;
        let j = 7;

        while (j > -1) {
            let approx = y + twoToThe[j];
            let approxSquared = approx * approx;
            // If approxSquared <= x and approxSquared does not overflow:
            if (~(approxSquared > x) & (0 < approxSquared)) { 
                let y = approx;
            }
            let j = j - 1;
        }

        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a < b) {
            return b;
        }
        return a;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }
        return x;
    }

    /** Returns the remainder of the division of x by y (this is used in
        the String and Screen modules) */
    function int mod(int x, int y){
        if (y = 0) {  // division by zero!
            do Output.printString("Error: Division by zero");
            do Sys.error(0);  // Division by zero
            return 0;
        }
        return x - Math.divide(x, y) * y;
    }

    /** Returns true if i-th bit of y is 1, and false otherwise */
    function boolean bit(int y, int i) {
        return ~((y & twoToThe[i]) = 0);
    }

    /** Returns the i-th power of 2 */
    function int powerOfTwo(int i) {
        if ((i < 0) | (i > 15)) {
            return 0;
        }
        return twoToThe[i];
    }
}